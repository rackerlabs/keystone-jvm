apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'spring-boot'

description = """keystone"""

group = 'org.openstack'
ext.featureVersion = '2014.2'
ext.buildVersion = System.currentTimeMillis()

version = "$featureVersion-$buildVersion"

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://repository.springsource.com/maven/bundles/release"
            url "http://repository.springsource.com/maven/bundles/external"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.6.RELEASE")
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://repository.springsource.com/maven/bundles/release"
        url "http://repository.springsource.com/maven/bundles/external"
    }
}

dependencies {
    compile project(path: ':jython', configuration: 'shadow')
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("com.h2database:h2")
    compile("org.projectlombok:lombok:1.14.8")
}

task copyLibPython(type: Copy) {
    dependsOn 'jython:jar'
    from fileTree('jython/jython/lib-python/2.7')
    into "python/Lib"
    exclude '**/*.pyc'
    exclude '**/*.pyo'
    setIncludes(file('jython/jython/CPythonLib.includes').readLines())
}

task copyJythonLib(type: Copy) {
    dependsOn copyLibPython
    from "$projectDir/jython/jython/Lib"
    into "python/Lib"
    exclude '**/*.pyc'
    exclude '**/*.pyo'
}

task compileall(type: JavaExec) {
    dependsOn copyJythonLib
    main = 'org.python.util.jython'
    args = ['-c', "import compileall; compileall.compile_dir('python/Lib')"]
    classpath configurations.runtime
    environment 'PYTHONPATH', "python/Lib"
    systemProperty 'install.root', "python"
    systemProperty 'python.home', "python"
    systemProperty 'python.path', "$projectDir"
    systemProperty 'python.executable', "$projectDir/python"
}

mainClassName = 'org.springframework.boot.loader.JarLauncher'

jar {
    archiveName = 'spring-boot.jar'
}

bootRepackage {
    dependsOn jar
    mainClass = 'org.openstack.Keystone'
    withJarTask = jar
}

task combinedJar(type: Jar) {
    archiveName = 'keystone.jar'
    dependsOn jar
    dependsOn compileall
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.JarLauncher'
        attributes 'Start-Class': 'org.openstack.Keystone'
        attributes 'Spring-Boot-Version': '1.1.6.RELEASE'
    }
    duplicatesStrategy = 'exclude'
    entryCompression = 'stored'
    from zipTree("build/libs/spring-boot.jar")
    from zipTree("jython/build/libs/jython-standalone.jar")
    into('Lib') {
        from fileTree("python/Lib")
    }
}

build.dependsOn combinedJar

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
